program "ensembl-webteam/core" "compara-scores" 1;
refer "libperegrine";
refer "libeoe";
include "../common/track-common.eard";
include "../common/track-style.eard";
include "compara-zmenu.eard";

/* setup styles */
let track_id = setting_string("track_id", []);
let track_display_order = setting_string("display_order", []);
track_styles();
new_track_style(track_id, track_display_order);

/* get data */
let track_datafile = setting_string("datafile", []);
let req = request("self()","compara-scores");
scope(req, "datafile", track_datafile);
let data = get_data(req);
halt(only_warm());

/* process data and draw wiggle */
let conservation_scores = data_number(data,"values");
let values = conservation_scores / 25; //range 0..25=>0..1
let x_range = data_number(data,"range"); //[startpos,endpos]


let outlier_colour = colour!("#fc0303"); // red
let normal_colour = colour!("#47d147"); // green

let normal_graph = graph_type(32, normal_colour); //graph(height,colour)
// let graph = graph_type(32, colour!("#47d147")); //graph(height,colour)
let leaf = track_leaf(track_id, "main/main/content");
wiggle(index(x_range,0), index(x_range,1), normal_graph, values, [true,...], leaf);

let is_min = values == 0;
let is_max = values == 1;

let outlier_graph = graph_type(32, outlier_colour); //graph(height,colour)
wiggle(index(x_range,0), index(x_range,1), outlier_graph, values, is_min || is_max, leaf);

/* Zmenu - Passes scores and positions as arguments */

// e.g. x_range = [32354272, 32354288]
// We are creating an array of positions from 32354272..32354288 using the enumerate function
// e.g. values = [8, 8, 15, 13, 9, 10, 11, 11, 11, 13, 11, 14, 12, 8, 12, 14]
let positions = enumerate([len(values)]) + index(x_range,0);

let conservation_score_zmenu_paint = conservation_score_zmenu(conservation_scores, positions);
let nw = coord(positions, [0,...], [0,...]);
let se = coord(positions + 1, [32,...], [0,...]); // Thinking that y=32 as height of the track and x is 1 base pair
rectangle(nw, se, conservation_score_zmenu_paint, [leaf,...]);

/* draw horizontal baseline */
let line_paint = paint_hollow(colour!("#d0d0d0"),1);
let bg_leaf = track_leaf(track_id,"main/background/content");
rectangle(coord([0],[16],[0]), coord([1],[16],[0]), line_paint, [bg_leaf,...]);

/* draw track furniture */
let track_name = setting_string("track_name", []);
draw_track_name(track_name, "name", track_leaf(track_id,"title/content"));

let info_leaf = track_leaf(track_id,"main/letter/content");
draw_track_category(["G"], [track_id], [info_leaf]);

/* draw scales on the left hand side near label "G"*/
let pen = pen("'Lato', sans-serif",10,[colour!("#666"),...],[colour!("transparent"),...]);
text(coord([0],[-5],[40]),pen,["25"],[info_leaf]); // position at the top of the track
text(coord([0],[24],[40]),pen,[" 0 "],[info_leaf]); // position at the bottom of the track
