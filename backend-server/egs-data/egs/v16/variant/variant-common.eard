export procedure focus_variant_settings() {
    (
        setting_string("focus-variant",["genome_id"]),
        setting_string("focus-variant",["variant_id"])
    )    
}

function summary_leaf(track_name,suffix) {
    leaf(join("/",["tracks/track",track_name,suffix]))
}

export function variant_colours() {
    [
        colour!("#fff"),    // 0 = white
        colour!("#EB768A"), // 1 = dark-pink
        colour!("#f8c041"), // 2 = dark-yellow
        colour!("#84fa3a"), // 3 = lime
        colour!("#327c89"), // 4 = teal
        colour!("#96d0c9")  // 5 = duckegg-blue
    ]
}

export procedure variant_summary_track(track_name,track_label) {
    let data = get_data(request("self()","variant"));
    halt(only_warm());

    let values = data_number(data,"values");
    let x_range = data_number(data,"range");

    let start = index(x_range,0);
    let end = index(x_range,1);
    let step = index(x_range,2) / 4000; // 4000 is multiple for precision applied in BE variant endpoint. (named SCALE there). Yuk!

    let pos_start = enumerate([len(values)]) * step + start;
    let pos_end = pos_start + step;

    let colours = variant_colours();

    let paint = paint_solid(index(colours,values));
    let leaf = summary_leaf(track_name,"main/main/content");
    rectangle(coord(pos_start,[0,...],[-1,...]),coord(pos_end,[5,...],[1,...]),paint,[leaf,...]);

    /* draw track furniture */

    draw_track_name(track_label,"name",summary_leaf(track_name,"title/content"));
    draw_track_category(["V"],["variant"],[summary_leaf(track_name,"main/letter/content")]);
}
