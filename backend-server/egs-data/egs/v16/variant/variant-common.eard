export procedure focus_variant_settings() {
    (
        setting_string("focus-variant",["genome_id"]),
        setting_string("focus-variant",["variant_id"])
    )    
}

export function variant_colours() {
    [
        colour!("#fff"),    // 0 = white
        colour!("#eb768a"), // 1 = dark-pink
        colour!("#f8c041"), // 2 = dark-yellow
        colour!("#84fa3a"), // 3 = lime
        colour!("#327c89"), // 4 = teal
        colour!("#96d0c9")  // 5 = duckegg-blue
    ]
}

export procedure variant_summary_track(track_id, track_label, track_datafile) {
    let req = request("self()", "variant-summary");
    scope(req, "datafile", track_datafile);
    let data = get_data(req);
    halt(only_warm());

    let values = data_number(data, "values");
    let x_range = data_number(data, "range");

    let start = index(x_range, 0);
    let end = index(x_range, 1);
    let step = index(x_range, 2) / 4000; // 4000 is multiple for precision applied in BE variant endpoint. (named SCALE there). Yuk!

    let pos_start = enumerate([len(values)]) * step + start;
    let pos_end = pos_start + step;

    let colours = variant_colours();

    let paint = paint_solid(index(colours,values));
    let leaf = track_leaf(track_id, "main/main/content");
    rectangle(coord(pos_start,[0,...],[-1,...]), coord(pos_end,[5,...],[1,...]), paint, [leaf,...]);

    /* draw track furniture */
    draw_track_furniture(track_id, track_label, "V");
}
