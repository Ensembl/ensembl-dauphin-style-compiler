program "ensembl-webteam/core" "focus-variant" 1;
refer "libperegrine";
refer "libeoe";
include "../common/track-common.eard";
include "../common/track-style.eard";
include "variant-common.eard";

/* Setup styles */

track_styles();

style!("""
        tracks/track/focus/ {
            priority: -900;
            report: "track;switch-id=focus;has-labels=true;!id";
        }

        tracks/track/focus/ {
            priority: -900;
            report: "track;switch-id=focus;has-labels=true;!id";
        }

        tracks/track/focus/main/main/shore/ {
            type: overlay;
            priority: 0;
        }

        tracks/track/focus/main/main/shore/under/ {
            depth: 0;
        }

        tracks/track/focus/main/main/shore/over/ {
            depth: 1;
        }
""");

/* Get settings */

let (focus_genome_id,focus_variant_id) = focus_variant_settings();

/* Get data */

let data = get_data(request("self()","variant"));
halt(only_warm());

let values = data_number(data,"values");
let x_range = data_number(data,"range");

let start = index(x_range,0);
let end = index(x_range,1);
let step = index(x_range,2) / 4000; // 4000 is multiple for precision applied in BE variant endpoint. (named SCALE there). Yuk!

/* Setup leafs */

function focus_leaf(path) {
    capture focus_genome_id;

    // XXX need someway of detecting what CHR we are on, but that needs data.
    leaf(path)
}

let leaf.bgd = focus_leaf("tracks/track/focus/main/background/content");
let leaf.text = focus_leaf(if(setting_boolean("focus-gene-label",[]),"tracks/track/focus/main/main/focus/text/content",""));
let leaf.letter = focus_leaf("tracks/track/focus/main/letter/content");
let leaf.trackname = focus_leaf("tracks/track/focus/title/content");
let leaf.shore_under = focus_leaf("tracks/track/focus/main/main/shore/under/content");
let leaf.shore_over = focus_leaf("tracks/track/focus/main/main/shore/over/content");

/* Draw shore */

let base_height = 7;
let base_paint = paint_hollow(colour!("#999"),0);
let colour_paint = paint_solid(index(variant_colours(),values));
let position = enumerate([end-start]) + start;

let nw = coord(position,[0,...],[2,...]);
let se = coord(position+1,[base_height,...],[-2,...]);
rectangle(nw,se,base_paint,[leaf.shore_over,...]);
rectangle(nw,se,colour_paint,[leaf.shore_under,...]);

/* Draw track furniture */

draw_track_category(["G"],["focus"],[leaf.letter]);
draw_track_name(focus_variant_id,"name",leaf.trackname);
draw_swatch(colour!("#58a8f9"),[leaf.letter]);
