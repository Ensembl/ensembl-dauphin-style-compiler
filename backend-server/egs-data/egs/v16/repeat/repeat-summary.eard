program "ensembl-webteam/core" "repeat-summary" 1;
refer "libperegrine";
refer "libeoe";
include "../common/track-common.eard";
include "../common/track-style.eard";

// Setup styles
let track_id = setting_string("track_id", []);
let track_display_order = setting_string("display_order", []);
setup_track(track_id, track_display_order);

// Get data
let track_datafile = setting_string("datafile", []);
let req = request("self()","repeat-summary");
scope(req, "datafile", track_datafile);
let data = get_data(req);
halt(only_warm());

// Draw blocks
let values = data_number(data, "values");
let x_range = data_number(data, "range");

let start = index(x_range, 0);
let end = index(x_range, 1);
let step = index(x_range, 2) / 1000;
let pos_start = enumerate([len(values)]) * step + start;
let pos_end = pos_start + step;

// Set colours based on repeat density value
let colours = [
	colour!("#ffffff"),
	colour!("#c9d4e0"),
	colour!("#8193a2")
];
let value_bins = if(values>150, [2,...], if(values>10, [1,...], [0,...]));
let paint = paint_solid(index(colours, value_bins));
let leaf = track_leaf(track_id, "main/main/content");
rectangle(coord(pos_start, [6,...], [0,...]), coord(pos_end, [14,...], [0,...]), paint, [leaf,...]);

// Draw track furniture
let track_name = join(" ", [setting_string("track_name", []), setting_string("additional_info", [])]);
draw_track_furniture(track_id, track_name, "G");