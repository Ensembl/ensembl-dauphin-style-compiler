program "ensembl-webteam/core" "gene-overview" 1;
refer "libperegrine";
refer "libeoe";
include "../common/track-common.eard";
include "../common/track-style.eard";
include "gene-common-data.eard";
include "gene-common-visual.eard";

/* Set style */

track_styles();
gene_styles();

style!("""
    **/main/main/ {
        type: overlay;
    }

    **/main/main/*/ {
        type: overlay;
        padding-top: 10;
    }

    **/main/main/*/blocks/ {
        depth: 4;
    }

    **/main/background/content {
        depth: -10;
        padding-top: 32;
    }
""");

/* Get data */

let (focus_genome_id,focus_gene_id) = focus_gene_settings();

/* this track uses two endpoints: gene-overview has the main data, but we also blank out the
 * track in the focus region for focs genes, so need to know where the focus gene is. */

let data = get_data(request("self()","gene-overview"));

let focus_request = request("self()","transcript");
scope(focus_request,"genome",focus_genome_id);
scope(focus_request,"id",focus_gene_id);
let focus_data = get_data(focus_request);

halt(only_warm());

/* Extract data */

let gene.start = data_number(data,"starts");
let gene.end = gene.start + data_number(data,"lengths");
let gene.biotype = data_string(data,"gene_biotypes");
let gene.strand = data_number(data,"strands");

let focus_gene.start = data_number(focus_data,"starts");
let focus_gene.end = focus_gene.start + data_number(focus_data,"lengths");
let focus_gene.biotype = data_string(focus_data,"gene_biotypes");
let focus_gene.id_unversioned = data_string(focus_data,"gene_id");

let enabled_tracks = enabled_gene_tracks();

/* Find start and end of focus gene */

let which_gene = focus_gene.id_unversioned==focus_gene_id;
let focus_gene.start = index(select(focus_gene.start,which_gene),0);
let focus_gene.end = index(select(focus_gene.end,which_gene),0);
let focus_gene.biotype = index(select(focus_gene.biotype,which_gene),0);

/* Which track should these genes go into? */

let gene.leaf_index = allocate_genes_to_leaf_index(*gene);

/* What colour should these genes be? */

let paint = make_gene_paint(*gene);

/* Calculate leafs */

let leaf.bgd = make_gene_leafs("main/background/content",enabled_tracks);
let leaf.letter = make_gene_leafs("main/letter/content",enabled_tracks);
let leaf.trackname = make_gene_leafs("title/content",enabled_tracks);
let leaf.main = make_gene_leafs("main/main/content",enabled_tracks);
let gene.leaf = index(leaf.main,gene.leaf_index);

/* Find focus gene by extent/track and blank it */
let gene.is_focus = (
    gene.start == focus_gene.start && 
    gene.end == focus_gene.end &&
    gene.biotype == focus_gene.biotype
);
let gene.leaf = set(gene.leaf,gene.is_focus,[leaf(""),...]);

/* Draw rectangles */

rectangle(coord(gene.start,[0,...],[-0.5,...]),coord(gene.end,[5,...],[0.5,...]),paint,gene.leaf);

/* Draw chevrons */

draw_track_chevrons(*leaf);
draw_sidebar_chevrons(*leaf);

/* Draw track furniture */

draw_gene_track_names(*leaf);
draw_gene_track_category(*leaf);
