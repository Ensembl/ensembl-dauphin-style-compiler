use "std";
use "peregrine";

module "common8";

stmt use_allotments(our_allotments,enabled_tracks,potential_tracks) {
    tracks_used := ["","","","",""];
    tracks_used[enabled_tracks] := potential_tracks[enabled_tracks];
    our_allotments := [use_allotment(tracks_used[])];
}

stmt use_allotment_names(our_allotments,enabled_tracks,potential_tracks) {
    tracks_used := ["","","","",""];
    tracks_used[enabled_tracks] := potential_tracks[enabled_tracks];
    our_allotments := [tracks_used[]];
}

gene_name_switches := [
    ["track","gene-other-rev","name"],
    ["track","gene-pc-rev","name"],
    ["track","gene-other-fwd","name"],
    ["track","gene-pc-fwd","name"],
    ["track","focus","name"],
];

gene_track_names := [
    "Genes and transcripts -- Other, Reverse strand",
    "Genes and transcripts -- Protein coding, Reverse strand",
    "Genes and transcripts -- Other, Forward strand",
    "Genes and transcripts -- Protein coding, Forward strand",
    ""
];

stmt draw_track_names(track_names,switch_names,allotments) {
    name_text_size := 10;
    name_text_colour := direct_colour(111,129,144,255);

    switches := get_switch(switch_names[]);
    label_allotments := allotments;
    label_allotments[switches==false] := use_allotment("");

    textpen_trackname := pen("'Lato', sans-serif",name_text_size,name_text_colour,[][]);
    text2(spacebase(len(track_names) (*) 0,0,8),textpen_trackname,track_names[],label_allotments[]);
}

stmt draw_track_category(category,allotments) {
    cat_text_size := 10;
    cat_text_colour := direct_colour(0,0,0,255);

    textpen_cat := pen("'Lato', sans-serif",cat_text_size,cat_text_colour,[][]);
    text2(spacebase(len(allotments) (*) 0,0,16),textpen_cat,category,allotments[]);
}

stmt draw_focus_swatch(allotment_value) {
    swatch_patina := patina_filled(simple_colour(direct_colour(88,168,249,255)));
    rectangle(spacebase(0,-9,0),spacebase(0,20,5),swatch_patina,allotment_value);
}

stmt red_dotted(pstart,pend,pallotment) {
    dots_patina := patina_hollow(barred(direct_colour(255,255,255,255),direct_colour(255,0,0,255),2,4,0.5),0.5);
    rectangle(spacebase(pstart,0,0),spacebase(pstart,-1,0),dots_patina,pallotment);
    rectangle(spacebase(pend,0,0),spacebase(pend,-1,0),dots_patina,pallotment);
}

style("dots/",["system","depth"][],["tracking-special","56"][]);
style("tracks/",["padding-top","priority"][],["16","0"][]);
style("tracks/track/*/",["padding-top","padding-bottom","height-adjust"][],["4","4","tracking"][]);
style("tracks/track/*/title/",["system","type","indent","padding-top","padding-bottom","priority"][],["window","overlay","left","2","2","-1000"][]);
style("tracks/track/*/main/",["type","priority"][],["overlay","0"][]);
style("tracks/track/*/main/letter/",["system","indent","depth"][],["window","datum(pad-left)","60"][]);
style("tracks/track/*/title/letter/",["system","indent","depth"][],["window","datum(pad-left)","60"][]);
style("tracks/track/*/main/main/",["system","priority"][],["tracking","1000"][]);
style("tracks/track/*/main/background/",["system"][],["window"][]);

stmt gene_track_styles() {
    style("tracks/track/gene-other-rev/",["priority","report","min-height"][],["110","track;switch-id=gene-other-rev;has-labels=true","92"][]);
    style("tracks/track/gene-pc-rev/",["priority","report","min-height"][],["100","track;switch-id=gene-pc-rev;has-labels=true","92"][]);
    style("tracks/track/gene-other-fwd/",["priority","report","min-height"][],["-110","track;switch-id=gene-other-fwd;has-labels=true","92"][]);
    style("tracks/track/gene-pc-fwd/",["priority","report","min-height"][],["-100","track;switch-id=gene-pc-fwd;has-labels=true","92"][]);
    style("tracks/track/focus/",["priority","report"][],["-900","track;switch-id=focus;has-labels=true"][]);
}
